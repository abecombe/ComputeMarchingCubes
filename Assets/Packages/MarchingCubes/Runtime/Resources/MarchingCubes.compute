#pragma kernel MeshReconstruction
#pragma kernel ComputeDispatchSize
#pragma kernel ClearUnused

#define SIZEOF_UINT 4
#define SIZEOF_FLOAT3 12

uint _TriangleBudget;
uint3 _GridSize;
float3 _Scaling; // Grid space to object space scaling
float _IsoValue;
StructuredBuffer<uint2> _TriangleTable;
StructuredBuffer<float> _DataBuffer;
RWByteAddressBuffer _VertexBuffer;
RWByteAddressBuffer _IndexBuffer;
RWStructuredBuffer<uint> _CounterBuffer;
RWStructuredBuffer<uint> _PrevCounterBuffer;
RWStructuredBuffer<uint> _ThreadCountBuffer;
RWStructuredBuffer<uint> _DispatchIndirectBuffer;

// Grid space to object space transformation
inline float3 TransformPoint(float3 p)
{
    return (p + 0.5 - _GridSize * 0.5f) * _Scaling;
}
inline uint EdgeIndexFromTriangleTable(uint2 data, uint index)
{
    return 0xfu & (index < 8 ? data.x >> ((index + 0) * 4) :
                               data.y >> ((index - 8) * 4));
}

//
// Input voxels
//
inline float VoxelValue(uint x, uint y, uint z)
{
    return _DataBuffer[x + _GridSize.x * (y + _GridSize.y * z)];
}

// Voxel value with calculated gradient
inline float4 VoxelValueWithGradient(uint3 i)
{
    uint3 i_n = max(i, 1) - 1;
    uint3 i_p = min(i + 1, _GridSize - 1);
    float v = VoxelValue(i.x, i.y, i.z);
    float v_nx = VoxelValue(i_n.x, i.y, i.z);
    float v_px = VoxelValue(i_p.x, i.y, i.z);
    float v_ny = VoxelValue(i.x, i_n.y, i.z);
    float v_py = VoxelValue(i.x, i_p.y, i.z);
    float v_nz = VoxelValue(i.x, i.y, i_n.z);
    float v_pz = VoxelValue(i.x, i.y, i_p.z);
    return float4(v_px - v_nx, v_py - v_ny, v_pz - v_nz, v);
}

//
// Output buffer and counter
//

// Vertex buffer accessor
inline void WriteVertex(uint offset, float3 p, float3 n)
{
    uint addr_p = offset * SIZEOF_FLOAT3 * 2;
    uint addr_n = addr_p + SIZEOF_FLOAT3;
    _VertexBuffer.Store3(addr_p, asuint(p));
    _VertexBuffer.Store3(addr_n, asuint(n));
}

// Index buffer accessor
inline void WriteIndices(uint offset, uint3 indices)
{
    _IndexBuffer.Store3(offset * SIZEOF_UINT, indices);
}

//
// Cube geometry/topology
//

// This must match one defined in Paul Bourke's article:
// http://paulbourke.net/geometry/polygonise/
inline uint3 CubeVertex(uint index)
{
    bool x = index & 1;
    bool y = index & 2;
    bool z = index & 4;
    return uint3(x ^ y, y, z);
}

inline uint2 EdgeVertexPair(uint index)
{
    // (0, 1) (1, 2) (2, 3) (3, 0)
    // (4, 5) (5, 6) (6, 7) (7, 4)
    // (0, 4) (1, 5) (2, 6) (3, 7)
    uint v1 = index & 7;
    uint v2 = index < 8 ? ((index + 1) & 3) | (index & 4) : v1 + 4;
    return uint2(v1, v2);
}

// Marching cube mesh reconstruction
[numthreads(4, 4, 4)]
void MeshReconstruction(uint3 id : SV_DispatchThreadID)
{
    uint i;

    // Boundary check
    if (any(id >= _GridSize - 1)) return;

    // Voxel samples at each cube vertex
    float4 samples[8];
    for (i = 0; i < 8; i++)
        samples[i] = VoxelValueWithGradient(id + CubeVertex(i));

    // Cube configuration selector
    // (initialized as a bit field of vertex binary states)
    uint selector = 0;
    for (i = 0; i < 8; i++)
        selector |= (samples[i].w < _IsoValue) << i;

    // Special case for empty cubes; Exit early if there is no intersection.
    if (selector == 0 || selector == 0xff) return;

    // Intersection points on each edge
    // We do this in a GPU-oriented way; Do the calculation on all the edges
    // including ones without an intersection.
    float3 vertices[12];
    float3 normals[12];
    for (i = 0; i < 12; i++)
    {
        uint2 pair = EdgeVertexPair(i);
        float4 sample1 = samples[pair.x];
        float4 sample2 = samples[pair.y];
        float3 vertex1 = id + CubeVertex(pair.x);
        float3 vertex2 = id + CubeVertex(pair.y);
        float param = (_IsoValue - sample1.w) / (sample2.w - sample1.w);
        vertices[i] = TransformPoint(lerp(vertex1, vertex2, param));
        normals[i] = -normalize(lerp(sample1.xyz, sample2.xyz, param));
    }

    // Output triangles in the selected cube configuration.
    uint2 tri_data = _TriangleTable[selector];
    for (i = 0; i < 15; i += 3)
    {
        uint e1 = EdgeIndexFromTriangleTable(tri_data, i);
        uint e2 = EdgeIndexFromTriangleTable(tri_data, i + 1);
        uint e3 = EdgeIndexFromTriangleTable(tri_data, i + 2);
        if (e1 == 15) return;

        uint count = _CounterBuffer.IncrementCounter();
        if (count >= _TriangleBudget) return;

        uint offset = count * 3;
        WriteVertex(offset + 0, vertices[e1], normals[e1]);
        WriteVertex(offset + 1, vertices[e2], normals[e2]);
        WriteVertex(offset + 2, vertices[e3], normals[e3]);
        WriteIndices(offset, uint3(offset, offset + 1, offset + 2));
    }
}

// Compute Dispatch for Cleanup kernel
[numthreads(1, 1, 1)]
void ComputeDispatchSize()
{
    uint count = min(_CounterBuffer[0], _TriangleBudget);
    uint thread_count = max(_PrevCounterBuffer[0], count) - count;

    _CounterBuffer[0] = count;
    _PrevCounterBuffer[0] = count;
    _ThreadCountBuffer[0] = thread_count;
    _DispatchIndirectBuffer[0] = (thread_count + 63) >> 6;
}

// Cleanup kernel that clears the unused area of the buffers
[numthreads(64, 1, 1)]
void ClearUnused(uint id : SV_DispatchThreadID)
{
    if (id >= _ThreadCountBuffer[0]) return;

    uint count = id + _CounterBuffer[0];

    uint offset = count * 3;
    WriteVertex(offset + 0, 0, 0);
    WriteVertex(offset + 1, 0, 0);
    WriteVertex(offset + 2, 0, 0);
    WriteIndices(offset, uint3(0, 0, 0));
}